* Семинар 5.

clear

set more off // отключить полный вывод результатов работы команд
cd /Users/polinapogorelova/Desktop/СМЭФ_Эконометрика/Эконометрика_25_26 // директория, в которой хранятся файлы
log using sem_5.log, text replace // начало записи результатов do-файла в sem_5.log

* Эндогенность

* CARD — это широко известный в эконометрике набор данных, который использовал Дэвид Кард (David Card) в своей влиятельной работе 1995 года, 
* посвященной оценке отдачи от образования с помощью инструментальных переменных. Данные основаны на выборке из Общенационального
* лонгитюдного обследования молодых мужчин (National Longitudinal Survey of Young Men) и относятся к волне 1976 года.
* Выборка включает 3010 работающих мужчин в возрасте от 24 до 34 лет. Основная идея исследования Карда заключалась в использовании 
* географической близости к колледжу как инструмента для оценки влияния образования на заработную плату, поскольку проживание рядом 
* с колледжем снижает стоимость получения образования, но напрямую не должно влиять на будущий заработок.

* Ниже приведено описание основных переменных датасета:
* Зависимая переменная: lwage: Логарифм почасовой заработной платы в 1976 году.
* Переменные, объясняющие заработную плату:
* educ: Количество лет образования.
* exper: Количество лет опыта работы на рынке труда.
* exprsq: Квадрат количества лет опыта работы.
* black: Дамми-переменная (1 — темнокожий, 0 — иначе).
* south76: Дамми-переменная (1 — проживание на юге США в 1976 году).
* smsa: Дамми-переменная (1 — проживание в стандартной метрополитенской статистической зоне (SMSA) в 1976 году).
* smsa66: Дамми-переменная (1 — проживание в SMSA в 1966 году).
* reg661-reg669: Девять дамми-переменных, указывающих регион проживания в 1966 году.
* Инструментальные переменные:
* nearc4: Дамми-переменная (1 — проживание рядом с четырехгодичным колледжем в 1966 году).
* fatheduc - образование отца (в годах)
* motheduc - образование матери (в годах)

use CARD.DTA
describe // описание набора данных
summarize // описательные статистики переменных

* 1. Метод наименьших квадратов
reg lwage educ exper expersq black south smsa smsa66 reg662 reg663 reg664 reg665 reg666 reg667 reg668 reg669
est store ols
predict yhat, xb // сохраняем прогноз по модели

* 2. Метод инструментальных переменных и 2-МНК
* Проанализируем, какие из имеющихся переменных могут быть инструментами для educ

* nearc4 - инструмент для educ
ivregress 2sls lwage (educ = nearc4) exper expersq black south smsa smsa66 reg662 reg663 reg664 reg665 reg666 reg667 reg668 reg669, first
est store iv

estat firststage // тестирование релевантности (качества) инструментов. H0: инструменты слабые
hausman iv ols // тест Хаусмана на проверку эндогенности регрессоров. H0: OLS-оценки. H1: IV-оценки

* nearc4 и nearc2 - инструменты для educ
ivregress 2sls lwage (educ = nearc4 nearc2) exper expersq black south smsa smsa66 reg662 reg663 reg664 reg665 reg666 reg667 reg668 reg669, first
est store iv2
estat firststage // тестирование релевантности (качества) инструментов. H0: инструменты слабые
estat overid // тестирование валидности (экзогенности) инструментов (l>=k) (тест Саргана). H0: все инструменты экзогенные (не коррелируют с ошибкой). H1: хотя бы один из инструментов эндогенный
hausman iv2 ols // тест Хаусмана на проверку эндогенности регрессоров. H0: OLS-оценки. H1: IV-оценки

* fatheduc, motheduc - инструменты для educ
ivregress 2sls lwage (educ = fatheduc motheduc) educ exper expersq black south smsa smsa66 reg662 reg663 reg664 reg665 reg666 reg667 reg668 reg669, first
est store iv3

* Тестирование инструментов
estat firststage // тестирование релевантности (качества) инструментов. H0: инструменты слабые
estat overid // тестирование валидности (экзогенности) инструментов (l>=k) (тест Саргана). H0: все инструменты экзогенные (не коррелируют с ошибкой). H1: хотя бы один из инструментов эндогенный
hausman iv3 ols // тест Хаусмана на проверку эндогенности регрессоров. H0: OLS-оценки. H1: IV-оценки

* nearc4, fatheduc, motheduc - инструменты для educ
ivregress 2sls lwage (educ = nearc4 fatheduc motheduc) educ exper expersq black south smsa smsa66 reg662 reg663 reg664 reg665 reg666 reg667 reg668 reg669, first
estat iv4
estat firststage // тест на релевантность инструментов
estat overid // тест Саргана
hausman iv4 ols // тест Хаусмана


* Модели бинарного выбора

* MROZ — это широко известный в эконометрике и статистике набор данных, основанный на исследовании T. A. Mroz 1987 года. 
* Он содержит информацию о 753 замужних женщинах, взятуую из волны 1976 года Панельного исследования динамики доходов (Panel Study of Income Dynamics, PSID) в США.
* Основная цель этого датасета — анализ предложения труда и факторов, влияющих на участие замужних женщин в рабочей силе.
* Данные представлены в нескольких распространенных версиях, которые различаются по набору переменных. Ниже приведено описание основных из них.
* Показатели занятости: находится ли женщина в составе рабочей силы (inlf), количество отработанных часов (hours).
* Демографические характеристики: возраст (age), количество детей младше 6 лет (kidslt6) и детей от 6 до 18 лет (kidsge6).
* Образование и доход: уровень образования жены и мужа в годах (educ, huseduc), заработная плата (wage), доход семьи (faminc).
* Дополнительные переменные: данные о родителях жены (образование матери motheduc и отца fatheduc), опыт работы (exper) и другие.

use http://www.stata.com/data/jwooldridge/eacsap/mroz, clear
describe
summarize
tab inlf city

logit inlf educ exper  age kidslt6 kidsge6 // оценивание logit-модели с помощью ММП (логистические шансы). Коэффициент при переменной показывает, во сколько раз изменится логарифм отношения P("удачи")/P("неудачи")
logit, or // odds ratio (отношение шансов)
predict xb0, xb // прогнозирование латентной переменной
predict pl0, pr // прогнозирование вероятности "успеха"
sum pl0 // описательная статистика предсказанной вероятности

test kidslt6 = kidsge6 = 0 // тестирование гипотезы о незначимости переменных, характеризующих состав детей в семье

margins, dydx(*) // усреднённые предельные эффекты (average marginal effects)
margins, dydx(*) atmeans // предельные эффекты для "среднего" наблюдения. ПЭ для переменной xj показывает, на сколько изменится вероятность "успеха" при увеличении xj на 1 единицу
margins, at(educ = (10(2)20)) // средняя вероятность "успеха" на каждом уровне educ от 10 до 20 лет с шагом 2 года
margins, at(educ = (10(2)20)) vsquish // средняя вероятность "успеха" на каждом уровне educ от 10 до 20 лет с шагом 2 года
margins, at(educ = (10(2)20)) atmeans // вероятность "успеха" на каждом уровне educ от 10 до 20 лет с шагом 2 года при средних значениях остальных переменных

* Сравним коэффициенты при образовании в logit и probit моделях
logit inlf educ exper age kidslt6 kidsge6 
est store logit_mod
local b_logit = _b[educ]

probit inlf educ exper  age kidslt6 kidsge6 
est store probit_mod
local b_probit = _b[educ]

* Считаем отношение
local ratio = `b_logit' / `b_probit'
display "Отношение logit-коэфф / probit-коэфф = " `ratio'


* Ordered logit regression

* Этот синтетический набор данных содержит переменную apply, имеющую три уровня: «маловероятно» (unlikely), 
* «отчасти вероятно» (somewhat likely) и «очень вероятно» (very likely), которые закодированы как 1, 2 и 3 соответственно. 
* Эта переменная будет использоваться в качестве зависимой (выходной) переменной. У нас также есть три переменные, которые 
* будут использоваться в качестве предикторов: pared (бинарная переменная: 1 — хотя бы один из родителей имеет диплом о 
* высшем образовании (магистра или доктора), 0 — нет); public (бинарная переменная: 1 — обучение на бакалавриате проходило 
* в государственном вузе, 0 — в частном) и gpa (средний балл успеваемости студента). 

use ologit.dta, clear
tab apply
tab apply, nolabel
tab apply public
sum gpa

ologit apply i.pared i.public gpa // оценим порядковый логит. Коэффициенты показывают во сколько раз увеличится логарифм отношения шансов при изменении объясняющей переменной на 1 единицу
brant, detail
ologit apply i.pared i.public gpa, or // получим оценки изменения отношения шансов (odds ratio) перейти с одного уровня на более высокий
listcoef, help

margins, at(pared = (0/1)) predict(outcome(0)) atmeans // прогнозируемая вероятность попасть в низшую категорию в зависимости от значения категориальной переменной "pared"
margins, at(pared = (0/1)) predict(outcome(1)) atmeans // прогнозируемая вероятность попасть в среднюю категорию в зависимости от значения категориальной переменной "pared"
margins, at(pared = (0/1)) predict(outcome(2)) atmeans // прогнозируемая вероятность попасть в высшую категорию в зависимости от значения категориальной переменной "pared"

forvalues i = 0/2 {
  margins, at(gpa = 3.5 pared = 1 public = 1) predict(outcome(`i'))
} // прогноз вероятности попасть в каждую категорию для индивидуума, имеющего GPA равный 3.5, обучавшегося в частной школе и у которого хотя бы один из родителей обучался в аспирантуре

net search omodel
omodel logit apply pared public gpa // альтернативная команда для оценивания упорядоченных моделей логит и пробит, содержащая также реультаты теста на пропорциональность шансов (test for the equivalence of the estimates for cut levels)
predict prob_unlikely prob_somewhatlikely prob_verylikely, pr // прогнозирование вероятностей попасть в каждую категорию


log close // завершаем запись в файл
